@name Discord to Gmod Link
@persist Bot:table

# Sends a message in chat whenever someone sends a message in a certain channel.
# You can also make it so it sends whenever someone says anything in any channel

@persist [BLACK PURPLE WHITE]:vector # Colors
@persist LINK_CHANNEL_ID

#include "vendetta"
if( first() ){
    # Constants
    BLACK = vec(0)
    PURPLE = vec(151,117,209)
    WHITE = vec(255)
    LINK_CHANNEL_ID = 809983770773684244

    Bot = discordBot("xxxxxxxx")
    # Put your discord bot token here.
    # Be aware if you use this stuff on a server, of course, you're literally running it on their server
    # They can see your bot token and everything.

    function on_message(Ctx:table){
        # https://discord.com/developers/docs/resources/channel#message-object
        local ChannelID = Ctx["channel_id",string]:toNumber()
        if(ChannelID==LINK_CHANNEL_ID){
            local Username = Ctx["author",table]["username",string]
            printGlobal(BLACK,"[",PURPLE,"Discord",BLACK,"] ",PURPLE, Username, WHITE,": ", Ctx["content",string])
        }
    }
    function on_guild(Ctx:table){
        # Received guild info
        printTable(Ctx)
    }
    function on_ready(Ctx:table){
        local User = Ctx["user",table]
        print(format("Logged in as bot %s#%s",User["username",string],User["discriminator",string]))
        # You receive a ton of information about the bot here.
    }
    Bot:vnAddIntent(VN_INTENT["GUILD_MESSAGES",number]) # Subscribe to getting message events (MESSAGE_CREATE, etc)

    Bot:vnOnEvent("READY","on_ready")
    Bot:vnOnEvent("MESSAGE_CREATE","on_message")
    Bot:vnConnect()
}elseif(chatClk()){
    local LS = lastSaid()
    # Todo... when E2 has the ability to send POST requests with headers.
}
